<!-- applyVulnResistJson user defined function -->
<!-- Returns: none -->
<!-- Parameters: 3 -->
<!-- arg0 : Json Array of alternating element/value that needs to be set in the passed property on the passed token -->
<!-- arg1 : Token name to look at -->
<!-- arg2 : Property to be set -->
<!-- Loop through passed Json Array collecting each alternating element name as a Key, and the integer as the Value and then do a -->
<!-- Json.set to set the passed Property object Json -->

[h:arrayJson       = arg(0)]
[h:tokenName       = arg(1)]
[h:propName        = arg(2)]

[h:propJson = getProperty(propName,tokenName)]
[h,if(length(propJson) <= 0): propJson = "{}"]
[h:propJson2 = propJson]
[h:tmpKey = ""]
[h:tmpVal = 0]
[for(i,0,json.length(arrayJson) - 1,2,""), CODE:
{
  [h:tmpKey = json.get(arrayJson,i)]
  [h:tmpVal = json.get(arrayJson,i+1)]
  [h:propVal = if(length(json.get(propJson,tmpKey)) > 0,json.get(propJson,tmpKey),0)]
  [h,if(tmpVal > propVal): propJson = json.set(propJson,tmpKey,tmpVal)]
}]
[h,if(json.equals(propJson,propJson2)!=1): setProperty(propName,propJson,tokenName)]